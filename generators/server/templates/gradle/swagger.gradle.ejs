<%#
 Copyright 2013-2018 the original author or authors from the JHipster project.

 This file is part of the JHipster project, see https://www.jhipster.tech/
 for more information.

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
-%>
/*
 * Plugin that provides API-first development using openapi-generator to
 * generate Spring-MVC endpoint stubs at compile time from an OpenAPI definition file
 */
 buildscript {
    dependencies {
        classpath('io.swagger:swagger-codegen:2.2.3')
    }
}

import io.swagger.codegen.config.CodegenConfigurator
import io.swagger.codegen.DefaultGenerator

def swaggerInput = 'src/main/resources/swagger/api.yml'
def swaggerOutputDir = file('build/openapi')

task generateApi {
    inputs.file(swaggerInput)
    outputs.dir(swaggerOutputDir)
    doLast {
        def config = new CodegenConfigurator()
        config.setInputSpec(swaggerInput)
        config.setOutputDir(swaggerOutputDir.path)
        config.setLang('spring')
        config.addDynamicProperty('interfaceOnly', true)
        config.addDynamicProperty('java8', true)
        config.setAdditionalProperties([
            'modelPackage'  : 'com.mycompany.myapp.web.api',
            'apiPackage'    : 'com.mycompany.myapp.web.api',
            'dateLibrary'   : 'java8'
        ])
        config.addSystemProperty('models', '')
        config.addSystemProperty('apis', '')
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}

clean.doFirst {
    delete(swaggerOutputDir)
}

configurations {
    swagger
}

sourceSets {
    swagger {
        compileClasspath = configurations.swaggerCompile
        java {
            srcDir file("build/openapi")
        }
    }
    main {
        compileClasspath += swagger.output
        runtimeClasspath += swagger.output
    }
    test {
        compileClasspath += swagger.output
        runtimeClasspath += swagger.output
    }
}

compileSwaggerJava.dependsOn generateApi
classes.dependsOn swaggerClasses
compileJava.dependsOn compileSwaggerJava
